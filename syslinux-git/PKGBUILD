# Maintainer : Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

_pkgname="syslinux"
pkgname="${_pkgname}-git"

pkgver=20120303
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Collection of boot loaders that boot from FAT, ext2/3/4 and btrfs filesystems, from CDs and via PXE - GIT Development version"
url="http://syslinux.zytor.com/"
license=('GPL2')
makedepends=('git' 'nasm' 'python2')
depends=('perl' 'glibc')
optdepends=('perl-passwd-md5: For md5pass'
            'perl-digest-sha1: For sha1pass'
            'mtools: For mkdiskimage and fat support'
            'dosfstools: For FAT filesystems support')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

backup=('boot/syslinux/syslinux.cfg')
install="${_pkgname}.install"
options=('strip' 'docs' 'libtool' '!emptydirs' 'zipman' 'purge')

source=('syslinux-dont-build-dos-windows-targets.patch'
        'syslinux-install_update'
        'syslinux.cfg')

sha256sums=('bb5e971a3c7c40fbb78391d9cace67366250b699ac3cdada1d0394bacb614031'
            'e2f5412d1f21ca868af8984b83d3a95223ced16554fdbf922135eee7ae97f660'
            '14d0d6ffe437cf42707b58198a00299a459e27fe5b7d7a65d75a4114e66506de')

_gitroot="git://git.kernel.org/pub/scm/boot/syslinux/syslinux.git"
_gitname="${_pkgname}"
_gitbranch="master"

_update_git() {
	
	cd "${srcdir}/"
	
	msg "Connecting to GIT server...."
	
	if [[ -d "${srcdir}/${_gitname}/" ]]; then
		cd "${srcdir}/${_gitname}/"
		git reset --hard
		git fetch
		git checkout "${_gitbranch}"
		git merge "remotes/origin/${_gitbranch}"
		msg "The local GIT repo has been updated."
	else
		git clone "${_gitroot}" "${_gitname}"
		cd "${srcdir}/${_gitname}/"
		git checkout "${_gitbranch}"
		msg "GIT checkout done or server timeout"
	fi
	
	echo
	
}

build() {
	
	_update_git
	
	rm -rf "${srcdir}/${_gitname}_build" || true
	cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}_build"
	
	cd "${srcdir}/${_gitname}_build"
	
	git reset --hard
	echo
	
	## Do not try to build syslinux with our default LDFLAGS, it will fail
	unset LDFLAGS
	
	## Do not try to build the Windows or DOS installers
	patch -Np1 -i "${srcdir}/syslinux-dont-build-dos-windows-targets.patch"
	echo
	
	## Fix FHS manpage path
	sed 's|/usr/man|/usr/share/man|g' -i "${srcdir}/${_gitname}_build/mk/syslinux.mk" || true
	
	# Use python2 instead of python
	sed 's|/usr/bin/python|/usr/bin/env python2|g' -i "${srcdir}/${_gitname}_build/com32/cmenu"/*.py || true
	sed 's|/usr/bin/env python|/usr/bin/env python2|g' -i "${srcdir}/${_gitname}_build/com32/cmenu"/*.py || true
	sed 's|python |python2 |g' -i "${srcdir}/${_gitname}_build/com32/cmenu/Makefile" || true
	
	msg "Starting make..."
	make
	echo
	
}

package() {
	
	cd "${srcdir}/${_gitname}_build/"
	make INSTALLROOT="${pkgdir}/" AUXDIR="/usr/lib/syslinux" install
	
	## move extlinux binary to /usr/sbin
	install -D -m0755 "${pkgdir}/sbin/extlinux" "${pkgdir}/usr/sbin/extlinux"
	rm "${pkgdir}/sbin/extlinux"
	
	## Install the default configuration
	install -D -m0644 "${srcdir}/syslinux.cfg" "${pkgdir}/boot/syslinux/syslinux.cfg"
	
	## Install the installation and update script
	## This script is maintained at git://gist.github.com/772138.git
	install -D -m0755 "${srcdir}/syslinux-install_update" "${pkgdir}/usr/sbin/syslinux-install_update"
	
}
